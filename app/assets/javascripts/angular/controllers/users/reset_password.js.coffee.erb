App.controller 'ResetPasswordCtrl', ['$scope', '$location', 'User', 'Flash',\
                                     ($scope, $location, User, Flash) ->
  $scope.id = $location.search().id
  $scope.token = $location.search().token
  $scope.activate = $location.search().activate

  if $scope.id && $scope.token
    $scope.user = User.ResetPassword.get({id: $scope.id, reset_password_token: $scope.token},
      (->
      ),
      (data, header) ->
        alert 'Your password reset email is invalid. Please request another.'
    )
  else
    alert 'Your password reset email is invalid. Please request another.'

  $scope.submitResetPassword = ->
    if $scope.password != $scope.password_confirm
      alert 'The password you entered and the confirmation do not match.'
      return

    User.ResetPassword.update({id: $scope.id, reset_password_token: $scope.token, password: $scope.password},
      (->
        Flash.set 'resetPassword', true
        $location.url '/auth/login'
      ),
      (data, header) ->
        if data.status == 422 # :unprocessable_entity
          $scope.errors = data.data.password
        else if data.status == 403 # :forbidden
          $scope.errors = ['and email not valid. Please request another.']
        else
          $scope.errors = ['had an unknown error. Please try again later.']
    )
]
