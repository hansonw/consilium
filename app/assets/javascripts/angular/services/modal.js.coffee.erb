App.factory 'Modal', ['$location', '$rootScope', ($location, $rootScope) ->
  modalService =
    toggleModal: (modalName, setMode) ->
      modalName = @_lastModalName if !modalName? || modalName == ''

      if !setMode? && !$('body').hasClass('modal-active') || setMode
        @_activatingModal = true

        # Huge hack to avoid 'shifting' content when scrollbar disappears.
        # WILL BREAK IF BODY HAS PADDING SET ELSEWHERE.
        width = $('body').prop('clientWidth')
        $('body').addClass('modal-active')
        newWidth = $('body').prop('clientWidth')
        $('body, #toolbar').css('padding-right', newWidth - width)
        $location.search('modalActive', true)
      else
        $('body').removeClass('modal-active')
        $('body, #toolbar').css('padding-right', 0)
        $location.search('modalActive', null)

      if modalName
        $(".modal[id~='modal-#{modalName}']").toggleClass('active', setMode)
        if $(".modal[id~='modal-#{modalName}']").hasClass('active')
          $("div#modal-#{modalName}").trigger("modal-toggle")

      @_lastModalName = modalName

    _lastModalName: '',
    _activatingModal: false,
    _locationChangeStart: ->
      if $('body').hasClass('modal-active') && !@_activatingModal
        @toggleModal('', false)
      @_activatingModal = false

  $rootScope.$on '$locationChangeStart', ->
    modalService._locationChangeStart()

  modalService
]
