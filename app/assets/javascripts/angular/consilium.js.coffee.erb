window.App = angular.module('Consilium', ['ngResource'])

App.config(['$routeProvider', ($routeProvider) ->
  #############
  # Home routes
  #############
  $routeProvider.when '/',
    templateUrl: '/app/templates/index.html'

  ################
  # Clients routes
  ################
  $routeProvider.when '/clients',
    templateUrl: '/app/templates/clients/index.html'
    $routeProvider.when '/clients/new',
      templateUrl: '/app/templates/clients/edit.html'
    $routeProvider.when '/clients/index',
      templateUrl: '/app/templates/clients/index.html',
    $routeProvider.when '/clients/recent',
      templateUrl: '/app/templates/clients/recent.html'
    $routeProvider.when '/clients/show/:clientId',
      templateUrl: '/app/templates/clients/show.html'
    $routeProvider.when '/clients/edit/:clientId',
      templateUrl: '/app/templates/clients/edit.html'

  #############
  # Auth routes
  #############
  $routeProvider.when '/auth/login',
    templateUrl: '/app/templates/auth/login.html'
    $routeProvider.when '/auth',
      templateUrl: '/app/templates/auth/login.html'

  $routeProvider.otherwise
    templateUrl: '/app/templates/index.html'
])

App.config(["$httpProvider", ($httpProvider) ->
  $httpProvider.defaults.headers.common['X-CSRF-Token'] = $('meta[name=csrf-token]').attr('content')
  $httpProvider.responseInterceptors.push('AuthHttpInterceptor')
])

App.run ($rootScope, $location, $anchorScroll, $timeout, $http, Client, Offline, Auth, AuthToken) ->

  # Initialize Auth service.
  Auth.init()

  # NOTE: This is for presentation purposes only. The flag indicating whether or not the user
  # is functionally logged in or not is in the Auth service.
  $rootScope.isLoggedIn = true
  $rootScope.logout = -> Auth.logout()
  Auth.watch (isLoggedIn) ->
    $rootScope.isLoggedIn = isLoggedIn
    $location.path(if isLoggedIn then '/' else '/auth/login')

  $rootScope.online_status = true
  $rootScope.last_sync_success = true

  $rootScope.checkOnline = ->
    online = Offline.online()
    if online != $rootScope.online_status
      if $rootScope.online_status = online
        # User just came online, so we should sync everything now.
        Client.sync(
          (-> $rootScope.last_sync_success = true),
          (-> $rootScope.last_sync_success = false)
        )
    else if !$rootScope.last_sync_success
      Client.sync(
        (-> $rootScope.last_sync_success = true),
        (-> $rootScope.last_sync_success = false)
      )
    $timeout($rootScope.checkOnline, 2000)

  $rootScope.checkOnline()
  # Also sync every time we navigate to a different page.
  $rootScope.$on('$routeChangeSuccess', -> Client.sync())

  $rootScope.scrollTo = (id) ->
    old = $location.hash()
    $location.hash(id)
    $anchorScroll()
    $location.hash(old)

  $rootScope.menuItems = [
    name: 'Create Client'
    link: '/clients/new',
  ,
    name: 'Search Clients'
    link: '/clients/index'
  ,
    name: 'Recent Clients'
    link: '/clients/recent'
  ]

  $rootScope._curMenuItem = null
  $rootScope._curSubMenuItem = null
  $rootScope._showMenu = false
  $rootScope._showSubMenu = false

  $rootScope.menuItemIsCaretExpanded = (path) ->
    return $rootScope._curMenuItem == path and $rootScope._showSubMenu

  $rootScope.menuShouldShow = ->
    return $rootScope._showMenu

  $rootScope.menuItemSelect = (path, subMenu) ->
    if $rootScope._curMenuItem == path and $rootScope._showSubMenu
      $rootScope._curMenuItem = null
    else
      $rootScope._curMenuItem = path
    if not $rootScope._showSubMenu = subMenu?
      $rootScope._showMenu = false if path?
      $location.path(path) if path?

  $rootScope.menuItemIsSelected = (path) ->
    return $location.path().substr(0, path.length) == path

  $rootScope.subMenuItemSelect = (path) ->
    $rootScope._showSubMenu = false
    $rootScope._showMenu = false
    if $rootScope._curSubMenuItem == path
      $rootScope._curSubMenuItem = null
    else
      $rootScope._curSubMenuItem = path

  $rootScope.subMenuShouldShow = (path) ->
    return $rootScope._curMenuItem == path and $rootScope._showSubMenu

  $rootScope.subMenuItemIsSelected = (path) ->
    return $location.path().substr(0, path.length) == path

  $rootScope.mainMenuLinkClick = ->
    $rootScope._showMenu = !$rootScope._showMenu
